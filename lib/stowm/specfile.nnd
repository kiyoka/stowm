;; #-*- mode: nendo; syntax: scheme -*-;;
;; specfile util functions

(use rfc.yaml)

;; create specfile as yaml.
;; stow-home: stow home path.
;; url: url arg must be validated.
;; return
;; ("yamlfile image"
;;  (( "filename of shellscript" . "shellscript image"))
;; )
(define (create-specfile stow-home url)
  (let* ([parsed (stowm-parse-url url)]
         [fetchable (not (eq? 'empty (scheme-type parsed)))]
         [tree
          `(
            ("fetch" . ,(if fetchable
                            (sprintf "wget %s -O %s" url (filename parsed))
                            #f))
            ("configure" . ,(sprintf "bash configure --prefix=%s/%s"
                                     stow-home
                                     (package parsed)))
            ("make" . "make")
            ("install" . "make install")
            ("url" . ,url)
            ("filename" . ,(filename parsed))
            ("projname" . ,(package parsed)))])
    
    (construct-yaml-string tree)))

;; load specfile
;; path : full path of specfile.yml
(define (load-specfile path)
  (with-open
   path
   (lambda (f)
     (parse-yaml f))))


;; return
;;   "tar zxf aaaa-1.2.3.tar.gz"
;; or
;;   "bzcat aaaa-1.2.3.tar.bz2 | tar xf -"
(define (create-extract-tarball-command url)
  (let* ([parsed (stowm-parse-url url)]
         [_filename (filename parsed)])
    (cond
     ((or (eq? 'gz (arc-type parsed))
          (eq? 'z (arc-type parsed)))
      (sprintf "tar zxf %s" _filename))
     ((eq? 'bz2 (arc-type parsed))
      (sprintf "bzcat %s | tar xf -" _filename))
     ((eq? 'xz (arc-type parsed))
      (sprintf "xzcat %s | tar xf -" _filename))
     (else
      (sprintf "echo \"Error: unsupported archive format. (%s) ; exit 1 ; \"" _filename)))))


;; create command line string
;; like "  cd ./ruby-2.2.0 && configure --prefix=XXXX && make && make install"
(define (create-build-cmdline cd cf mk ins)
  (let ([cf (if (stowm-regex-match "^[ \t]*$" cf)
                #f
                cf)]
        [mk (if (stowm-regex-match "^[ \t]*$" mk)
                #f
                mk)]
        [ins (if (stowm-regex-match "^[ \t]*$" ins)
                #f
                ins)])
    (string-join
     (filter-map
      (lambda (x) x)
      (list cd cf mk ins))
     " && ")))

;; spec object to Makefile string
;; spec-obj : loaded data of specfile.yml
(define (create-makefile spec-obj home-repos)
  (let* ([fetch (assv-ref "fetch" spec-obj)]
         [filename (assv-ref "filename" spec-obj)]
         [projname (assv-ref "projname" spec-obj)]
         [strings
          (list
           (sprintf "all: %s/configure\n	%s"
                    projname
                    (create-build-cmdline
                     (sprintf "cd ./%s" projname)
                     (assv-ref "configure" spec-obj)
                     (assv-ref "make" spec-obj)
                     (assv-ref "install" spec-obj)))
           (sprintf "%s/configure : %s\n	%s\n	touch %s/configure"
                    projname
                    filename
                    (create-extract-tarball-command (assv-ref "url" spec-obj))
                    projname)
           (if fetch
               (sprintf "%s :\n	%s"
                        filename
                        fetch)
               (sprintf "%s :\n	/bin/cp %s/%s/%s %s"
                        filename
                        home-repos
                        projname
                        filename
                        filename))
           (sprintf "clean:\n	/bin/rm -rf ./%s\n"
                    projname))])
    (string-join
     strings
     "\n\n")))

