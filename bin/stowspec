#!/bin/sh
true; #-*- mode: nendo; syntax: scheme -*-;;
true; exec /usr/local/bin/nendo $0 $*

(define debug-print-length 1024)
(define pp pretty-print)

(define archive-file-pattern "([a-zA-Z].*)")
(define stow-home   (File.expand_path "~/stowspec"))
(define stow-target "/usr/local/stow")
(define stow-bin    "/usr/local/bin")
(define stow-temp   "/tmp/stowspec")

;; `regex' is a string ( not a regex object )
;; returns ( $0-string  $1-string  $2-string  $3-string ... )
(define (regex-match regex str)
  (let1 matchdata ((. str match) regex)
    (if matchdata
        (matchdata.to_a.to_list)
        nil)))

;; test
;;(regex-match "^[a-z]+$" "abc")
;;(regex-match "^([a-z]+)([0-9]+)$" "abc123")


(require "digest/md5")

(define (exec-sh script)
  (let* ((digest (Digest::MD5.hexdigest script))
         (tmpfile (sprintf "/tmp/stowspec_script.%s.sh" digest)))
    (with-open
     tmpfile
     (lambda (f)
       (f.puts script))
     "w")
    (print "---BEGIN---")
    (display script)
    (.system (+ "bash " tmpfile))
    (print "---END---")))


(define (tgz-to-name tgz)
  (cond
   ((regex-match (+ "^.+[/]" archive-file-pattern "(.tar.gz|.tgz|.tar.bz2)$") tgz)
    => second)
   ((regex-match (+ "^"   archive-file-pattern "(.tar.gz|.tgz|.tar.bz2)$") tgz)
    => second)
   (else
    nil)))

;; test
(when false
  (list
   (tgz-to-name "abc-1.2.3.tar.gz")
   (tgz-to-name "abc-1.2.3.tar.bz2")
   (tgz-to-name "abc-1.2.3.tgz")
   (tgz-to-name "file.txt")
   (tgz-to-name "http://ftp.gnu.org/gnu/wget/wget-1.9.tar.gz")
   ))


(define (get-project-basename name-and-ver)
  (cond
   ((regex-match (+ "^([A-Za-z0-9-]+)[-][0-9]+") name-and-ver)
    => second)
   (else
    "???")))

;; test
(when false
  (list
   (get-project-basename "abc-1.2.3.tar.gz")
   (get-project-basename "abc-1.2.3.tar.bz2")
   (get-project-basename "abc-1.2.3.tgz")
   (get-project-basename "file.txt")
   (get-project-basename "wget-1.9.tar.gz")
   (get-project-basename "w3m-0.5.2")
   (get-project-basename "emacs-w3m-20081218-CVS")
   (get-project-basename 
    (tgz-to-name "http://ftp.gnu.org/gnu/wget/wget-1.9.tar.gz"))
   ))

(define (get-envs)
  (let* ((pwd (ENV.fetch "PWD"))
         (in-project
          (cond
           ((regex-match (+ "^" stow-home "/" archive-file-pattern "$") pwd)
            => second)
           (else
            nil))))
    `(
      (pwd         . ,pwd)                  ;; pwd
      (home        . ,(ENV.fetch "HOME"))   ;; "/home/xxxx/"
      (stow-home   . ,stow-home)            ;; "/home/xxxx/stowspec"
      (target      . ,stow-target)          ;; "/usr/local/stow"
      (bin         . ,stow-bin)             ;; "/usr/local/bin"
      (temp        . ,stow-temp)            ;; "/tmp/stowspec"
      (project     . ,in-project)           ;; nil or "aaaa-1.2.3.tar.gz"
      )))


(define (get-curdir-info envs)
  (let1 pwd (assq-ref 'pwd  envs)
    (cond
     ((assq-ref 'project envs)
      => (lambda (x)
           `(specdir . x)))
     (else
      `(other . "")))))


(define assoc-to-local
  (macro (_assoc name)
    `(,name (assq-ref (quote ,name) ,_assoc))))

;; test
(when false
  (let1 _aaa '((filename . "FILENAME")
               (projname . "PROJNAME")
               (url      . nil))
    (pretty-print
     (macroexpand
      '(let
           (
            (assoc-to-local _aaa filename)
            (assoc-to-local _aaa projname)))))
    (let
        (
         (assoc-to-local _aaa filename)
         (assoc-to-local _aaa projname))
      (+ filename ":" projname))))
      

(define (gen-script envs project-envs spec)
  (let* ((_ (append envs project-envs))
         (assoc-to-local _ projname)
         (assoc-to-local _ projpath)
         (assoc-to-local _ arc)
         (assoc-to-local _ workdir)
         (assoc-to-local _ url))
    (begin
      `(
        (store . ,(string-join
                   (list
                    (sprintf "mkdir -p %s" projpath)
                    (if url
                        (sprintf "wget -O %s/%s %s"      projpath arc url)
                        (sprintf "/bin/cp %s %s"         arc projpath))
                    (sprintf "cd %s" projpath)
                    "")
                   "\n"))
        (build . ,(string-join
                   (list
                    (sprintf "/bin/rm -rf %s/%s" workdir projname)
                    (sprintf "mkdir -p %s" workdir)
                    (sprintf "/bin/cp -f %s %s" arc workdir)
                    (sprintf "cd %s" workdir)
                    (sprintf "tar zxf %s" arc)
                    (sprintf "cd ./%s"    projname)
                    (assq-ref 'conf       spec)
                    (assq-ref 'make       spec)
                    (assq-ref 'install    spec)
                    "")
                   "\n"))
        (stowspec . ,(string-join
                      (list
                       (sprintf "cd %s" projpath)
                       "stowspec"
                       "")
                      "\n"))
        ))))


(define (get-dir-entries path)
  (filter
   (lambda (x)
     (not (or (x.match "[/]?[.]$")
              (x.match "[/]?[.][.]$")
              (x.match "^[.]"))))
   (to-list (Dir.entries path))))

;;(get-dir-entries "/usr/local/stow")


(define (get-linklist envs)
  (let* ((bin-path (assq-ref 'bin envs))
         (bin-list
          (map
           (lambda (x)
             (+ bin-path "/" x))
           (get-dir-entries bin-path))))
    (filter-map
     (lambda (x)
       (when (File.symlink? x)
         (File.readlink x)))
     bin-list)))
  

;; check installed binary file by stow.
(define (binary-installed? envs linklist projname)
  (if (not projname)
      false
      ;; checking installed file counts.
      (< 0 (length
            (filter
             (lambda (x)
               (regex-match projname x))
             linklist)))))

(define (get-stowed-list envs)
  (let* ((projs    (get-dir-entries (assq-ref 'stow-home envs)))
         (stowed   (get-dir-entries (assq-ref 'target envs)))
         (all      (uniq (sort (append projs stowed))))
         (linklist (get-linklist envs))
         (cnt      0))
    (map
     (lambda (x)
       (let1 result
           (cons x
                 (list
                  (if (binary-installed? envs linklist x) 'i false)
                  (if (memv x stowed) 's false)
                  (if (memv x projs ) 'p false)
                  (get-project-basename x)
                  cnt))
         (begin
           (set! cnt (+ cnt 1))
           result)))
     all)))

(when false
  (get-stowed-list
   '(
     (pwd . "/Volumes/CaseSensitive/kiyoka/work/github/nendo/sample")
     (home . "/Users/kiyoka")
     (stow_home . "/Users/kiyoka/stowspec")
     (target . "/usr/local/stow")
     (bin . "/usr/local/bin")
     (temp . "/tmp/stowspec")
     (project))))


(define (stowed-list-to-tree lst)
  (let1 program-list
      (uniq (sort (map
                   (lambda (x)
                     (fifth x))
                   lst)))
    (map
     (lambda (x)
       (cons
        x
        (filter (lambda (z)
                  (eq? (fifth z) x))
                lst)))
     program-list)))

(define (listing stowed-list)
  (define (ox bool-val)
    (if bool-val "O" "-"))
  (define _format_h "       %-25s %10s %10s %10s\n")
  (define _format_d "   %2d) %-25s %10s %10s %10s\n")

  (print
   (apply +
          (cons
           (sprintf _format_h "       <name>" "<enabled>" "<installed>" "<source>")
           (map
            (lambda (tree)
              (apply +
                     (cons
                      (sprintf " %s \n" (car tree))
                      (map
                       (lambda (x)
                         (sprintf _format_d
                                  (sixth x)
                                  (+ " " (first x))
                                  (ox (second x))
                                  (ox (third x))
                                  (ox (fourth x))))
                       (cdr tree)))))
            (stowed-list-to-tree stowed-list))))))
    

;; test
;;(stowed-list-to-tree '(("Gauche-0.8.14" false s p "Gauche" 0) ("Gauche-0.9" i s p "Gauche" 1) ("bash-4.0" false s p "bash" 2) ("bash-4.1" i s p "bash" 3) ("mosh-0.2.0" false false p "mosh" 4) ("ruby-1.8.7-p174" false s p "ruby" 5) ("ruby-1.9.1-p376" false s p "ruby" 6) ("ruby-1.9.2-preview1" i s p "ruby" 7) ("simstring-1.0" false false p "simstring" 8) ("subversion-16" false false p "subversion" 9) ("tinyscheme1.39" false false p "tinyscheme" 10) ("tokyocabinet-1.4.42" i s p "tokyocabinet" 11) ("wget-1.11.4" i s p "wget" 12)))



(define stowspec nil)
(define (define-stowspec alist)
  (set! stowspec alist))
  
(define (action-by-curdir envs)
  (let1 curdir-info (get-curdir-info envs)
    (cond
     ((eq? 'specdir (car curdir-info))
      ;; ----------
      ;; build!
      ;; ----------
      (if (not (File.exist? "./specfile"))
          (begin
            (print "Error: ./specfile does not exist...")
            (exit 1))
          (begin
            (load "./specfile")
            (let* ((spec stowspec)
                   (project-envs (get-project-envs envs
                                                   (assq-ref 'projname spec)
                                                   (assq-ref 'arc      spec)
                                                   (assq-ref 'url      spec))))
              (exec-sh
               (assq-ref 'build (gen-script
                                 envs
                                 project-envs
                                 spec)))))))
     (else
      ;; ----------
      ;; show current stowed status
      ;; ----------
      (listing 
       (get-stowed-list envs)))
     )))


(define (get-project-envs envs projname arc url)
  (let1 projpath (+ (assq-ref 'stow-home envs) "/" projname)
    `(
      (projname . ,projname)
      (projpath . ,projpath)
      (workdir  . ,(sprintf "%s/%s" (assq-ref 'temp envs) projname))
      (arc      . ,arc)
      (url      . ,url)
      (specfile . ,(+ projpath "/specfile")))))


(define (action-by-number envs number)
  (let* ((lst (get-stowed-list envs))
         ;; ----------
         ;; search the number in list
         ;; ----------
         (found
          (filter
           (lambda (x)
             (= number (sixth x)))
           lst)))
    (if (null? found)
        (begin
          (printf "Error: selected number [%d] was not found...\n" number)
          (exit 1))
        (let* ((basename (fifth (car found)))
               (tree (stowed-list-to-tree lst))
               (selection (assv-ref basename tree)))
          (let ((delete-item
                 (filter-map
                  (lambda (x)
                    (if (= 'i (second x))
                        (car x)
                        nil))
                  selection))
                (restow-item
                 (filter-map
                  (lambda (x)
                    (if (and (= 's (third x))
                             (= number (sixth x)))
                        (car x)
                        nil))
                  selection)))
            (cond
             ((null? restow-item)
              (printf "Error: selected number [%d] was not installed yet. \n" number)
              (exit 1))
             ((equal? delete-item restow-item)
              (printf "Info: selected program [%s] was already restowed. \n" (car restow-item))
              (exit 0))
             (else
              (let1 script (string-join
                            (list
                             (sprintf "cd %s" (assq-ref 'target envs))
                             (if (null? delete-item)
                                 ""
                                 (sprintf "stow -D %s" (car delete-item)))
                             (if (null? restow-item)
                                 ""
                                 (sprintf "stow -R %s" (car restow-item)))
                             "")
                            "\n")
                (exec-sh script)))))))))



(define (action-by-arg envs argv)
  (let* ((m1 (regex-match (+ "^http://.+/" archive-file-pattern "$") (car argv)))
         (m2 (regex-match (+ "^" archive-file-pattern "$") (car argv)))
         (url      (if m1 (car m1) nil))
         (arc      (if m1 (second m1)
                       (if m2 (car m2) nil)))
         (projname (or (tgz-to-name arc) "")))
    (let* ((project-envs (get-project-envs envs projname arc url))
           (assoc-to-local project-envs workdir)
           (assoc-to-local project-envs specfile)
           (assoc-to-local project-envs projpath))
      (if (or url arc)
          (begin
            (printf "url      = %s\n" url)
            (printf "arc      = %s\n" arc)
            (printf "projname = %s\n" projname)
            (printf "workdir  = %s\n" workdir)
            (printf "specfile = %s\n" specfile)
            (let* ((scripts  (gen-script envs project-envs '())))
              (exec-sh (assq-ref 'store scripts))
              (printf "Info: specfile = %s\n" specfile)
              (printf "cd %s\n" projpath)
              (with-open specfile
                         (lambda (f)
                           (f.puts ";;-*- mode: scheme; syntax: scheme -*-;;")
                           (f.puts "(define-stowspec '(")
                           (for-each
                            (lambda (x)
                              (f.puts (+ " " (write-to-string x))))
                            `((conf      .
                                         ,(string-join
                                           (list
                                            "if [ ! -e ./configure ] ; then"
                                            "  echo 'Error: stowspec supports ./configure script included tar ball only, sorry...'"
                                            "  exit 1"
                                            "fi"
                                            (sprintf "./configure --prefix=%s/%s " (assq-ref 'target envs) projname)
                                            "")
                                           "\n"))
                              (make      . "make")
                              (install   . "make install")
                              (arc       . ,arc)
                              (url       . ,url)
                              (projname  . ,projname)))
                           (f.puts "))"))
                         "w")
              (exec-sh (assq-ref 'stowspec scripts))))
          (begin
            (print "Error: please specify url by argument.")
            (exit 1))))))
          
;; test
;;(action-by-arg '("a.tar.gz"))
;;(action-by-arg '("http://www.example.com/a.tar.gz"))


(define (main argv)
  (let1 envs (get-envs)
    (case (length argv)
      ((0)
       (action-by-curdir envs))
      (else
       (cond
        ((regex-match "^[0-9]+$" (car argv))
         (action-by-number envs (to-i (car argv))))
        (else
         (action-by-arg    envs argv)))))))

(main (cdr ARGV.to_list))
